apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'
apply from: '../build_ext_functions.gradle'

ext.th_keystore_pwd = getKeystorePassword()
ext.th_key_pwd = getKeyPassword()

android {
    compileSdkVersion propCompileSdkVersion

    defaultConfig {
        applicationId "dv.gallery.photos.album.photoeditor.fotos"
        minSdkVersion propMinSdkVersion
        targetSdkVersion propTargetSdkVersion
        versionCode propVersionCode
        versionName propVersionName
        multiDexEnabled true

        ndk {
            abiFilters "armeabi-v7a", "x86", 'arm64-v8a', 'x86_64'
        }
    }

    signingConfigs {
        release {
            storeFile file("../../../keystore/dv_gallery.release.jks")
            storePassword th_keystore_pwd
            keyAlias "dv_gallery"
            keyPassword th_key_pwd
        }
        debug {
            storeFile file("../../../keystore/common.debug.jks")
        }
    }

    splits {
        density {
            enable true
            exclude "ldpi", "mdpi", "xxxhdpi"
        }
    }

    buildTypes {
        debug {
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            shrinkResources true
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/library_release.kotlin_module'
    }
}


android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = outputFileName.replace(".apk", "-${variant.versionName}.apk")
    }
}

// package apk and mapping to output folder
android.applicationVariants.all { variant ->
    if (variant.buildType.name != "release") {
        return
    }
    variant.productFlavors.each { flavor ->
        def version = "${android.defaultConfig.versionCode}-${android.defaultConfig.versionName}"
        def destination = "${rootDir}/../../output/${flavor.name}/${version}"

        tasks.create(name: "archive${variant.name.capitalize()}", type: Copy) {
            def apkDir = "${buildDir}/outputs/apk/${flavor.name}/${variant.buildType.name}"
            def mappingDir = "${buildDir}/outputs/mapping/${flavor.name}/${variant.buildType.name}"

            println "====> apkDir: " + apkDir

            from(apkDir) {
                include "*${flavor.name}-xxhdpi-${variant.buildType.name}-${variant.versionName}.apk"
                rename 'xxhdpi-', ''
            }
            from(mappingDir) {
                include "mapping.txt"
            }
            into destination

            doFirst {
                file(destination).deleteDir()
            }
        }
    }
}

dependencies {
    implementation project(':common')
    implementation project(':thCommon')
    implementation project(':GestureViews')
    implementation project(':SubsamplingScaleImageView')

    implementation 'com.google.firebase:firebase-core:17.2.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'it.sephiroth.android.exif:library:1.0.1'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.22'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.10.3'
    implementation 'com.google.vr:sdk-panowidget:1.180.0'
    implementation 'com.google.vr:sdk-videowidget:1.180.0'
    implementation 'org.apache.sanselan:sanselan:0.97-incubator'
    implementation 'info.androidhive:imagefilters:1.0.7'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.caverock:androidsvg-aar:1.3'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    kapt 'com.github.bumptech.glide:compiler:4.9.0' // keep it here too, not just in Commons, else loading SVGs wont work

    kapt 'androidx.room:room-compiler:2.1.0'
    implementation 'androidx.room:room-runtime:2.1.0'
    annotationProcessor 'androidx.room:room-compiler:2.1.0'
}

apply plugin: 'com.google.gms.google-services'